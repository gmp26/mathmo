module audioPlayer

import mobl::ui::stylemixin
import mobl::ui::generic
import mobl::media

resource jungleloop.mp3

style playerBoxStyle {
  border: 1px solid #444;
  padding: 20px;
  borderRadiusMixin(10px);
  background: white;
  width: 80%;
  margin-left: auto;
  margin-right: auto;
  text-align: center;
}

control sound() {
  header("Audio")
  table {
    row {
      headerCol { "MP3 supported: " }
      col { label(Audio.canPlayMp3() ? "Yes" : "No")}
    }
    row {
      headerCol { "AAC supported: " }
      col { label(Audio.canPlayAac() ? "Yes" : "No")}
    }
    row {
      headerCol { "Ogg Vorbis supported: " }
      col { label(Audio.canPlayVorbis() ? "Yes" : "No")}
    }
    row {
      headerCol { "Wave supported: " }
      col { label(Audio.canPlayWav() ? "Yes" : "No")}
    }
  }
  when(Audio.canPlayMp3()) {
    var audio = Audio.load("jungleloop.mp3")
    block(playerBoxStyle) {
      button("Play", onclick={
        audio.play();
      })
      button("Pause", onclick={
        audio.pause();
      })
      button("Reset", onclick={
        audio.currentTime = 0;
      })
      label(Math.round(audio.currentTime))
      block {
        button("Louder", onclick={
          audio.volume = Math.min(1, audio.volume + 0.1);
        })
        label(Math.round(audio.volume*10))
        button("Softer", onclick={
          audio.volume = Math.max(0, audio.volume - 0.1);
        })
      }
    }
  }
}